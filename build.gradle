plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.9'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// ───────────────────────────────────────────
// Q‑클래스 생성 디렉토리 & clean 설정
def generated = 'src/main/generated'

// 1) JavaCompile 시 GENERATED 소스 디렉토리 지정
tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory.set(file(generated))
}

// 2) clean 시 GENERATED 디렉토리 삭제
clean {
    delete file(generated)
}

// 3) QClass 생성 디렉토리를 Java 소스에 추가, 컴파일 대상 포함시킴
sourceSets {
    main {
        java {
            srcDirs += [ generated]
        }
    }
}

// ───────────────────────────────────────────

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        // Spring Cloud 2023.0.5 (Leyton) 트레인 – Spring Boot 3.3.x 호환
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.5"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation("com.google.api-client:google-api-client:2.8.0")
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
    implementation 'com.google.apis:google-api-services-calendar:v3-rev20250404-2.0.0'

    //Swagger-ui
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    // Thymeleaf
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'

    // JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6' // 타임리프에서 시큐리티

    // @retry 사용하기 위한 의존성 추가
    implementation("org.springframework.retry:spring-retry")

    //p6spy
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'

    // openfeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    developmentOnly("org.springframework.boot:spring-boot-devtools")

    // Query Dsl
    implementation "com.querydsl:querydsl-jpa:5.0.0:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Validator
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // apache.sshd
    implementation 'org.apache.sshd:sshd-core:2.7.0'
}

tasks.named('test') {
    useJUnitPlatform()
}
